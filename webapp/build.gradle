/*
 * gcUnicorn
 * Copyright (C) 2018  Martin Misiarz
 *
 * This program is free software; you can redistribute it and/or
 * modify it under the terms of the GNU General Public License version 2
 * as published by the Free Software Foundation.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA  02110-1301, USA.
 */

import java.text.SimpleDateFormat

plugins {
    id 'org.springframework.boot'
    id 'io.spring.dependency-management'
    id 'org.jetbrains.kotlin.jvm' version '1.6.10'
    id 'org.jetbrains.kotlin.plugin.spring' version '1.6.10'
    id 'org.jetbrains.kotlin.plugin.noarg' version '1.6.10'
}

ext {
    // Spring Boot.
    versionSpringBoot = '2.2.4.RELEASE'
}

repositories {
    maven {
        url = 'https://repo.spring.io/libs-milestone'
    }
}

bootRun {
    systemProperty 'spring.profiles.active', 'development'
}

bootJar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    manifest {
        attributes(
                "Created-By" : project.maintainer,
                "Created-On" : new SimpleDateFormat("yyyy-MM-dd HH:mm:ss").format(new Date()),
                "Implementation-Title" : project.name,
                "Implementation-Version" : project.version,
                "Build-JDK" : System.getProperty('java.version'),
                "Source-Compatibility" : project.sourceCompatibility,
                "Target-Compatibility" : project.targetCompatibility,
                "License" : "GPL-2.0"
        )
    }
}

kotlin {
    jvmToolchain {
        languageVersion.set(JavaLanguageVersion.of(8))
    }
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.boot:spring-boot-dependencies:$versionSpringBoot"
    }
}

dependencies {
    implementation project(':gcUnicorn-core')
    annotationProcessor(
            'org.springframework.boot:spring-boot-configuration-processor'
    )
    implementation('org.springframework.boot:spring-boot-starter') {
        exclude group: 'org.yaml', module: 'snakeyaml'
    }
    implementation(
            'org.springframework.boot:spring-boot-starter-undertow',
            'org.springframework.boot:spring-boot-starter-thymeleaf',
            'org.springframework.boot:spring-boot-starter-security',
            // WAITING: Switch to RELEASE once it is out.
            'org.springframework.mobile:spring-mobile-device:2.0.0.M3',
            'org.springframework:spring-webmvc',
            'org.springframework:spring-messaging',
            'org.springframework:spring-websocket',
            'org.springframework.security:spring-security-messaging'
    )
    runtimeOnly(
            "com.fasterxml.jackson.module:jackson-module-kotlin:$versionJackson"
    )
    compileOnly(
            'org.springframework.boot:spring-boot-devtools'
    )
    testImplementation(
            "org.testng:testng:$versionTestNG",
            'org.springframework:spring-test'
    )
}

compileKotlin.dependsOn(processResources)

test {
    useTestNG()
}
